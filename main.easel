pub prop owner.Score = 0
pub accumulator owner.HighestScore = 0

pub prop owner.Level
pub collect World.MaxLevel ~ FindMax

pub game fn World.Main(maxHumanPlayers=5, allowRejoining=true) {
    SolidBackground(color=#181122)
    Camera(@(0,0), radius=BoundaryRadius, aspectRatio=1)

    once PlayerJoined {
        await PlayerJoined // Wait for a second player to join before displaying a scoreboard
        
        SidebarContent {
            Scoreboard(&Score)
        }
    }

    SpawnEachPlayer owner {
        Subspawn ship {
            Ship

            once BeforeDespawn {
                Eliminate
            }
        }

        with Score {
            Level = 1 + Floor(Sqrt(Score / 100))
            give MaxLevel(Level)
        }

        once AfterGameCommenced {
            with Level {
                TopTransmission(duration=3s) {
                    H1(animate=Animate:FlyFromBottom) { "Level " + Level }
                }
            }
        }
        
        TopContent {
            with Score {
                if Score > 0 {
                    H1 { %(Score) }
                } else {
                    VStack(align=Align:Center) {
                        P {
                            Span(bold=true, color=#00ff00) { "Click" }
                            " to accelerate, and press "
                            Span(bold=true, color=#00ff00) { "Spacebar" }
                            " to fire"
                        }
                    }
                }
            }
        }

        once AfterEliminated {
            if Score > HighestScore {
                Transmission {
                    P(color=#00ff00) { "New high score: " + Score + "!" }
                }
                HighestScore(overwrite=Score, showOnLeaderboard=true)
            } else {
                Transmission {
                    P(color=#00ff00) { "Your highest score is still " + HighestScore + ". Better luck next time!" }
                }
            }
                
            OverlayContent {
                VStack(align=Align:Center) {
                    Standout { FlyInWords("Game Over") }
    
                    HStack(animate=Animate:FlyFromBottom, align=Align:Center) {
                        RaisedButton(LeaderboardPage, backgroundColor=Color:Secondary) { "High Scores" }
                        RaisedButton(Main, backgroundColor=Color:Primary) { "Play Again" }
                    }
                }
            }
        }
    }

    once AfterGameCommenced {
        behavior<spawnAsteroids> with Tick(2s) {
            const MaxAsteroids = 5 * MaxLevel
            if QueryCount(filter=Category:Asteroid) < MaxAsteroids {
                Spawn asteroid {
                    Asteroid
                }
            }
        }

        behavior<spawnPowerups> with Tick(5s) {
            const MaxPowerups = (MaxLevel - 1).Clamp(0, 5)
            if QueryCount(filter=Category:Powerup) < MaxPowerups {
                Spawn powerup {
                    surprise {
                        ShieldPowerup,
                        TriblastPowerup,
                    }
                }
            }
        }

        behavior<spawnAliens> with Tick(10s) {
            const MaxAliens = (MaxLevel - 2).Max(0)
            if QueryCount(filter=Category:Alien) < MaxAliens {
                Spawn ship {
                    Alien
                }
            }
        }
    }

    once AfterGameConcluded {
        delete behavior<spawnAsteroids>
        delete behavior<spawnPowerups>
        delete behavior<spawnAliens>
    }

    on PlayerEliminated {
        if !QueryAnyPlayer(isEliminated=false) {
            ConcludeGame
            break
        }
    }
}